(* HealthLang — EBNF v1.2 *)

PROGRAM    = { STATEMENT } ;

STATEMENT  = IF | WHILE | ADJUST | ACTION | WAIT | LOG | ";" ;

IF         = "if" "(" BOOL ")" BLOCK [ "else" BLOCK ] ;
WHILE      = "while" "(" BOOL ")" BLOCK ;
BLOCK      = "{" { STATEMENT } "}" ;

(* condições compostas *)
BOOL       = SIMPLE { LOGOP SIMPLE } ;
SIMPLE     = SENSOR COMPARISON NUMBER ;
LOGOP      = "and" | "or" ;
COMPARISON = "<" | "<=" | ">" | ">=" | "==" | "!=" ;

(* ajustes de controle *)
ADJUST     = "ajustar_O2"   "(" NUMBER ")" ";"
           | "ajustar_soro" "(" NUMBER ")" ";"
           | "aumentar_O2"  "(" NUMBER ")" ";"
           | "reduzir_O2"   "(" NUMBER ")" ";"
           | "aumentar_soro" "(" NUMBER ")" ";"
           | "reduzir_soro"  "(" NUMBER ")" ";" ;

(* ações binárias *)
ACTION     = "alerta" "(" ")" ";"
           | "silenciar" "(" ")" ";" ;

WAIT       = "esperar" "(" NUMBER ")" ";" ;
LOG        = "log" "(" SENSOR ")" ";" ;

SENSOR     = "oxigenio" | "batimento" | "intravenoso" ;

NUMBER     = DIGIT { DIGIT } ;
DIGIT      = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;